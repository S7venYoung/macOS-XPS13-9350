#Maintained by: RehabMan for: Laptop Patches
#battery_Dell-Inspiron-15-7xxx.txt

# created by lisai9093, 2015-04-04

# works for:
#  Dell Inspiron 15 7000 Series (late 2014)
#  Dell Inspiron 14 7000 Series (14-7447 confirmed, courtesy isnowbee)

into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized) { Return(Or(Arg0, ShiftLeft(Arg1, 8))) }\n
end;

# 16-bit registers
into device label EC code_regex CAP0,\s+16 replace_matched begin DAP0,8,DAP1,8 end;
into device label EC code_regex VOT0,\s+16 replace_matched begin WOT0,8,WOT1,8 end;
into device label EC code_regex CRT0,\s+16 replace_matched begin DRT0,8,DRT1,8 end;
into device label EC code_regex FCP0,\s+16 replace_matched begin GCP0,8,GCP1,8 end;
into device label EC code_regex DCP0,\s+16 replace_matched begin ECP0,8,ECP1,8 end;
into device label EC code_regex DVT0,\s+16 replace_matched begin EVT0,8,EVT1,8 end;
into device label EC code_regex BSN0,\s+16 replace_matched begin CSN0,8,CSN1,8 end;

# fix 16-bit methods
into method label _BST code_regex \(\^\^EC\.CAP0, replaceall_matched begin (B1B2(^^EC.DAP0,^^EC.DAP1), end;
into method label _BST code_regex \(\^\^EC\.VOT0, replaceall_matched begin (B1B2(^^EC.WOT0,^^EC.WOT1), end;
into method label _BST code_regex \(\^\^EC\.CRT0, replaceall_matched begin (B1B2(^^EC.DRT0,^^EC.DRT1), end;
into method label _BIF code_regex \(\^\^EC\.FCP0, replaceall_matched begin (B1B2(^^EC.GCP0,^^EC.GCP1), end;
into method label _BIF code_regex \(\^\^EC\.DCP0, replaceall_matched begin (B1B2(^^EC.ECP0,^^EC.ECP1), end;
into method label _BIF code_regex \(\^\^EC\.DVT0, replaceall_matched begin (B1B2(^^EC.EVT0,^^EC.EVT1), end;
into method label _BIF code_regex \(\^\^EC\.BSN0, replaceall_matched begin (B1B2(^^EC.CSN0,^^EC.CSN1), end;

